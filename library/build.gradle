plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

ext {
    libraryVersionCode = 4
    libraryVersionName = "1.1.0"
}

android {
    compileSdkVersion 30
    buildToolsVersion '29.0.3'

    resourcePrefix "nppc_"


    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
        versionCode libraryVersionCode
        versionName libraryVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-beta01', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.h6ah4i.android.widget:numberpickercompat:1.0.0'
//    implementation project(':android-numberpickercompat')
    testImplementation 'junit:junit:4.12'
}

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = 'com.h6ah4i.android.preference'
                artifactId = 'numberpickerprefcompat'
                version = libraryVersionName

                from components.all

                pom {
                    name = 'NumberPickerPreferenceCompat'
                    description = 'A Preference widget with NumberPicker dialog. Works on API level 14+ devices.'
                    url = 'https://github.com/h6ah4i/android-numberpickerprefcompat'
                    packaging = 'aar'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'h6ah4i'
                            name = 'Haruki Hasegawa'
                        }
                    }
                    scm {
                        connection = 'git:https://github.com/h6ah4i/android-numberpickerprefcompat.git'
                        developerConnection = 'git:git@github.com:h6ah4i/android-numberpickerprefcompat.git'
                        url = 'https://github.com/h6ah4i/android-numberpickerprefcompat'
                    }
                }
            }
        }
        repositories {
            maven {
                name = 'BuildDir'

                def releasesRepoUrl = "$buildDir/repos/releases"
                def snapshotsRepoUrl = "$buildDir/repos/snapshots"
                url = libraryVersionName.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }

            maven {
                name = 'MavenCentral'

                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                url = libraryVersionName.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = "${project.SONATYPE_NEXUS_USERNAME}"
                    password = "${project.SONATYPE_NEXUS_PASSWORD}"
                }
            }
        }
    }

    signing {
        useGpgCmd()
        sign publishing.publications.mavenJava
    }
}

